#!retranse -i
#meta retranse-dialect A
# **************************************************************************** #
# * This is retranse sample #5                                                 *
# * english to greek language translation                                      *
# *                                                                            *
# * written by Kimon Kontosis                                                  *
# **************************************************************************** #

function unmark ( (.*) )
{
  (.*)([^\s])+[{|\[][^\s]*[}|\]](.*) $1$2$3 
  rerun
}

function greekspell ( (.*) )
{
  "(.*) (το|τη)ν (.[^κπταάεέιίυύηήοόωώϋϊΰΐ].*)" "$1 $2 $3"
  "(.*) σε το([ν]*) (.*)" "$1 στο$2 $3"
  "(.*) σε τη([ν]*) (.*)" "$1 στη$2 $3"
  rerun
}

function words ( (.*) )
{
  black{m} 		μαύρος[m]
  black{f} 		μαύρη[f]
  black{o} 		μαύρο[o]
  μαύρος{o} 	μαύρο[o]

  brown 		καφέ[u]

  quick{m} 		γρήγορος[m]
  quick{f} 		γρήγορη[f]
  quick{o} 		γρήγορο[o]
  γρήγορος{o} 	γρήγορο[o]
  
  lazy{m} 		τεμπέλης[m]
  lazy{f} 		τεμπέλα[f]
  lazy{o} 		τεμπέλικο[o]
  τεμπέλης{o} 	τεμπέλη[o]

  cat 			γάτα[f] 
  
  dog 			σκύλος[m] 
  σκύλος{o} 	σκύλο[o]

  fox 			αλεπού[f] 
  
  I 			εγώ[u]
  me 			εμένα[u]
  
  moon			φεγγάρι[o]
  
  am 			είμαι[verb]
  
  bark  		γαυγίζω[verb]
  barks 		γαυγίζει[verb] 

  eat  			τρώω[verb]
  eats 			τρώει[verb] 

  "jump" 		πηδάω[verb]
  jumps 		πηδάει[verb]

  run  			τρέχω[verb]
  runs 			τρέχει[verb] 
  
  at 			σε[..]
  to 			σε[..]
  towards 		προς[..]
  
  beneath 		"κάτω[.] από[..]"
  near 			"κοντά[.] σε[..]"
  over 			"επάνω[.] από[..]"
  under 		"κάτω[.] από[..]"
  
  and 			και[;;]
  
  a{f} 			μία[f]   	
  a{m} 			ένας[m]  	
  ένας{o} 		έναν[o] 	
  
  the{m} 		ο[m] 		
  the{f} 		η[f] 		
  the{o} 		το[o] 		
  ο{o} 			τον[o]
  η{o} 			την[o]
  
  this{f} 		αυτή[f]
  this{m} 		αυτός[f]
  this{o} 		αυτό[f]
  αυτός{o} 		αυτό[o]
    
  cond ( $(00) == $0 ) reduce to $0[?]
}

function pairs ( (.*) )
{
  "this is{f}" 				"αυτή[f] είναι[is]"  
  "this is{m}" 				"αυτός[m] είναι[is]" 
  
  "this (.*[{|\[]f[}|\]])" 	"αυτή[f] η[f] $1" 	
  "this (.*[{|\[]m[}|\]])" 	"αυτός[m] ο[m] $1" 	
}

function translate ( (.*) )
{
  # is + preposition
  "(.*) ([^\s\[\{]+)\[(.?)\] is[{|\[].?[}|\]]? ([^\s\[\{]+)\[\.\] (.*)" "$1 $2[$3] είναι[verb] $4{$3} $5"

  # to be
  "(.*) ([^\s\[\{]+)\[(.?)\] is[{|\[].?[}|\]]? ([^\s\[\{]+)\[\?\] (.*)" "$1 $2[$3] είναι[is] $4{$3} $5"

  # subject - verb - object
  "(.*) ([^\s]+) ([^\s\[\{]+\[verb\][^;]*) ([^\s\[\{]+)\[[m|f|o]\] (.*)" "$1 $2 $3 $4{o} $5"

  # pair of words
  (.*)\s([^\s\[\{]+)\s([^\s]+)\s(.*) $1:$2:$3:$4 [B] {
    call a = pairs ( "$2 $3" )
	(.*):(.*):(.*):(.*) $1\s$(a)\s$4
  }

  # single word
  (.*)\s([^\s\[]+)\s(.*) $1:$2:$3 [B] {
	call a = words ( $2 )
	(.*):(.*):(.*) $1\s$(a)\s$3
  }

  # adjective gender
  "(.*) ([^\s\[\{]+)\[\?\] ([^\s\[\{]+)\[([mfo]?)\] (.*)" "$1 $2{$4} $3[$4] $5"
  "(.*) ([^\s\[\{]+)\[u\] ([^\s\[\{]+)\[(.?)\] (.*)" "$1 $2[$4] $3[$4] $5"
  
  rerun
}

call t = translate ( " $0 " )
call u = unmark ( $(t) )
call s = greekspell ( $(u) )
.* $(s)
\s(.*)\s $1


